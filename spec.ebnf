newline        = /* the Unicode code point U+000A */ .
unicode_char   = /* an arbitrary Unicode code point except newline */ .
unicode_letter = /* a Unicode code point classified as "Letter" */ .
unicode_digit  = /* a Unicode code point classified as "Number, decimal digit" */ .

letter         = unicode_letter | "_" .
filename    = { [ "/" ]  { unicode_letter } } .
ipaddr      = unicode_digit { unicode_digit } "."
              unicode_digit { unicode_digit } "."
              unicode_digit { unicode_digit } "."
              unicode_digit { unicode_digit } "." .
port        = unicode_digit { unicode_digit } .
networkaddr = ipaddr ":" port .
location    = filename | networkaddr .
schema      = "file" | "tcp" | "udp" | "unix" .
uri         = schema "://" location .

identifier  = letter { letter | unicode_digit } .
variable    = "$" identifier .
builtin     = importDecl | rforkDecl | cdDecl | ifDecl | elseDecl |
              forDecl | execDecl | setenvDecl | showenvDecl | fnDecl .
importDecl  = "import" ( filename | string_lit ) .
rforkDecl   = "rfork" rforkFlags "{" program "}" .
namespace   = { "c" | "m" | "n" | "i" | "p" | "u" | "s" } .
rforkFlags  = { namespace } .
cdDecl      = "cd" [ filename ] .
ifDecl      = "if" ( identifier | varValue ) comparison ( identifier | varValue ) "{" program "}" .
elseDecl    = "else" "{" program "}" .
forDecl     = "for" ( identifier | varList ) "{" program "}" .
setenvDecl  = "setenv" identifier .
showenvDecl = "showenv" .
execDecl    = "exec" (varList | variable) { redirect } .
fnDecl      = "fn" identifier "(" fnArgs ")" "{" program "}" .
redirect    = ( ">" ( filename | uri ) |
               ">" "[" unicode_digit "]" ( filename | uri ) |
               ">" "[" unicode_digit "=" unicode_digit "]" |
               ">" "[" unicode_digit "=" "]" ) .

comparison  = "==" | "!=" .

string_lit = "\"" { unicode_char | newline } "\"" .

varDecl  = identifier "=" varSpec .
varSpec  = ( varList | varValue ) .
varValue = ({ letter | unicode_digit }) | string_lit .
varList  = "(" { varValue } ")" .

argument  = ( unicode_char { unicode_char } ) | string_lit .
cmdpart   = [ "-" ] ( cmdname | abscmd ) { argument } { redirect } .
pipe      = cmdpart "|" cmdpart [ { "|" cmdpart } ] .
command   = ( cmdpart | pipe ) .
cmdname   = identifier .
abscmd    = filename .

fnArg     = ( string_lit | variable ) .
fnArgs    = { fnArg [ "," ] } .

comment   = "#" { unicode_char } .

program   = { statement } .
statement = varDecl | command | builtin | comment .
